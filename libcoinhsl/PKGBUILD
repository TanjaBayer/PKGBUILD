# Maintainer: Benjamin Chretien <chretien@lirmm.fr>

pkgname=libcoinhsl
pkgver="2012.12.21"
pkgrel=1
pkgdesc="A number of HSL linear solvers that can be used in IPOPT"
arch=(i686 x86_64)
url="http://www.hsl.rl.ac.uk/ipopt/"

# HSL offers 3 types of licence:
# academic, commercial (per seat) and commercial (incorporation)
# See: http://www.hsl.rl.ac.uk/licencing.html
license=('custom:HSL Academic Licence')

depends=()
makedepends=()
provides=('libcoinhsl')
conflicts=()

# Add the HSL Academic Licence
source=('license.txt')
md5sums=('38f73d0e7e8984cc8eaa5007fc97dae0')

# HSL routines are not openly available. One needs to apply for an academic or
# commercial licence on their website.
# See: http://www.hsl.rl.ac.uk/catalogue/

# This PKGBUILD was tested with HSL for IPOPT: http://www.hsl.rl.ac.uk/ipopt/

# !!! Source code location has to be specified by the user !!!
#_COINHSL_PATH=
_COINHSL_NAME="coinhsl-${pkgver}"

build() {
    # Check if the user provides a source archive
    if [ -z ${_COINHSL_PATH} ] ; then
        error "Missing user-provided archive name"
        return 1
    fi

    msg "Unpacking user-provided COIN-HSL sources..."
    tar -xf ${_COINHSL_PATH}/${_COINHSL_NAME}.tar

    cd $srcdir/${_COINHSL_NAME};

    msg "Configuring installation..."
    ./configure --prefix=/usr

    msg "Compiling libcoinhsl..."
    make -j1
}

package() {
    cd $srcdir/${_COINHSL_NAME};
    msg "Installing libcoinhsl..."
    make DESTDIR=$pkgdir install
    
    # Copy some Fortran source files to /usr/share/coin/libcoinhsl since
    # Ipopt rely on some of those files (e.g. hsl_ma77_double.mod)
    mkdir -p "$pkgdir/usr/share/coin/libcoinhsl"
    cp *.mod "$pkgdir/usr/share/coin/libcoinhsl/"
}
