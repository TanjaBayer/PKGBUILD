# Maintainer: Benjamin Chretien <chretien at lirmm dot fr>
pkgname=urdfdom
pkgver=0.2.10
pkgrel=1
pkgdesc="The URDF (U-Robot Description Format) library provides core data structures and a simple XML parsers for populating the class data structures from an URDF file."
arch=('i686' 'x86_64')
url="http://gazebosim.org/"
license=('BSD')
depends=( 'boost' 'tinyxml' 'console-bridge' 'urdfdom-headers')
makedepends=('git' 'cmake')

_gitroot=https://github.com/ros
_gitrepo=urdfdom

_tag=${pkgver}
_dir=${_gitrepo}
source=("${_dir}"::"git+${_gitroot}/${_gitrepo}.git"#tag=${_tag})
md5sums=('SKIP')

build() {
  # Create build directory
  [ -d ${srcdir}/build ] || mkdir ${srcdir}/build
  cd ${srcdir}/build

  # Build project
  cmake ${srcdir}/${_dir} \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX="/usr"
  make
}

package() {
  cd ${srcdir}/build
  make DESTDIR="$pkgdir/" install
}
