# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=the_platinum_searcher
pkgver=1.7.1
pkgrel=1
pkgdesc="A code search tool similar to ack and the_silver_searcher(ag)"
arch=('i686' 'x86_64')
makedepends=('git' 'go' 'mercurial')
url="https://github.com/monochromegane/the_platinum_searcher"
license=('MIT')
provides=('pt' 'the_platinum_searcher')

build() {
  GOPATH="$srcdir" go get -u github.com/monochromegane/the_platinum_searcher/...
}

check() {
  [[ -f /etc/profile.d/go.sh ]] && source /etc/profile.d/go.sh
  GOPATH="$GOPATH:$srcdir" go test -v -x github.com/monochromegane/the_platinum_searcher/ || return 0
}

package() {
  [[ -f /etc/profile.d/go.sh ]] && source /etc/profile.d/go.sh

  msg 'Installing srclibs...'
  install -dm 755 "$pkgdir/usr/share/the_platinum_searcher/src"
  for _lib in `find "$srcdir/src" -mindepth 1 -maxdepth 1 -printf '%f\n'`; do
    cp -dpr --no-preserve=ownership "$srcdir/src/$_lib" "$pkgdir/usr/share/the_platinum_searcher/src/$_lib"
  done

  msg 'Installing pkglibs...'
  install -dm 755 "$pkgdir/usr/share/the_platinum_searcher/pkg"
  for _pkg in `find "$srcdir/pkg" -mindepth 1 -maxdepth 1 -printf '%f\n'`; do
    cp -dpr --no-preserve=ownership "$srcdir/pkg/$_pkg" "$pkgdir/usr/share/the_platinum_searcher/pkg/$_pkg"
  done

  msg 'Installing binaries...'
  for _bin in `find "$srcdir/bin" -mindepth 1 -maxdepth 1 -type f -printf '%f\n'`; do
    install -Dm 755 "$srcdir/bin/$_bin" "$pkgdir/usr/bin/$_bin"
  done

  msg 'Cleaning up pkgdir'
  find "$pkgdir" -type d -name .git -exec rm -r '{}' +
  find "$pkgdir" -type f -name .gitignore -exec rm -r '{}' +
  find "$pkgdir" -type d -name .hg -exec rm -r '{}' +
  find "$pkgdir" -type f -name .hgignore -exec rm -r '{}' +
}
